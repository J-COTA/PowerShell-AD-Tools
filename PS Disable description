#The following will disable, change description.

On Error Resume Next
Const ADS_SCOPE_SUBTREE = 2
Const ForReading = 1
Const ForAppending = 8

Set objConnection = CreateObject("ADODB.Connection")
Set objCommand =   CreateObject("ADODB.Command")

objConnection.Provider = "ADsDSOObject"
objConnection.Open "Active Directory Provider"
Set objCommand.ActiveConnection = objConnection

objCommand.Properties("Page Size") = 10000
objCommand.Properties("Searchscope") = ADS_SCOPE_SUBTREE 

Set objFSO = CreateObject("Scripting.FileSystemObject")
Set objTextFile = objFSO.OpenTextFile("FILEPATH.txt" , ForReading)

Set objFSO = CreateObject("Scripting.FileSystemObject")
strFile = "c:\Export\DisableResults.log "

If NOT objFSO.FileExists(strFile) Then
  Set objFile = objFSO.CreateTextFile(strFile)
  objFile.Close
End If

Set objTextFile2 = objFSO.OpenTextFile(strFile , ForAppending)

Do While objTextFile.AtEndOfStream <> True
  strUser = objtextFile.ReadLine

  objCommand.CommandText = _
    "SELECT * FROM 'LDAP://DC=domain,DC=com' WHERE sAMAccountName='" & strUser & "'"
  Set objRecordSet = objCommand.Execute

  objRecordSet.MoveFirst

  Do Until objRecordSet.EOF
    Set objUser = GetObject(objRecordSet.Fields("AdsPath").Value)
    Dim x

    'Change Text to Prepend to the Description
    x = "TEXT; " & objUser.Description

    objUser.AccountDisabled = True
    objUser.Put "description", x
    objUser.SetInfo

    Set objUser = Nothing
    objRecordSet.MoveNext
  Loop

  If err.number <> 0 Then
    objTextFile2.WriteLine strUser & ",Failed"
  Else
    objTextFile2.WriteLine strUser & ",Succeeded"
  End If

  err.clear
Loop
